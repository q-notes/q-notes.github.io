---
layout: post
title:  "ring"
date:   2018-06-25 20:10:00 +0700
category: clojure
tags:   clojure ring web
---

![ring]({{ "/assets/ring.jpeg" | absolute_url }})

- handler fn: [req](https://github.com/ring-clojure/ring-spec/blob/master/src/ring/core/spec.clj#L115) -> [resp](https://github.com/ring-clojure/ring-spec/blob/master/src/ring/core/spec.clj#L146)
- middleware fn: handler -> handler
  + request middleware
  ```clj
  (defn wrap-user [handler user-info]
    (fn [request]
      (handler
        (assoc request :user user-info))))
  ```
  + response middleware
  ```clj
  (defn wrap-content-type [handler content-type]
    (fn [request]
      (let [response (handler request)]
        (assoc-in response [:headers "Content-Type"] content-type))))
  ```
  + req+resp middleware
  ```clj
  (defn wrap-cookies [handler options]
    (fn [request]
      (-> request
          (cookies-request options)
          handler
          (cookies-response options))))
  ```
- middleware order: bottom up
  ```clj
  ; let said we have 2 middleware
  (defn wrap-1 [handler]
    (fn [req]
      (handler
        (assoc req :a 1))))

  (defn wrap-2 [handler]
    (fn [req]
      (handler
        (assoc req :a 2))))

  ; when use it, order will be apply for wrap-2 first, then wrap-1
  ; it seem confuse at first, as it's reverse with -> order
  (-> handler
      wrap-1
      wrap-2)

  ; let see why
  (wrap-2 (wrap-1 handler))

  (wrap-2 #(handler (assoc % :a 1)))

  (fn [req]
    (#(handler (assoc % :a 1))
      (assoc req :a 2)))

  (fn [req]
    (handler (assoc (assoc req :a 2) :a 1)))

  ; so after all, fn from wrap-2 is apply to req first, then wrap-1
  ```
